tree grammar SchemaWalker;

options {
  language = Java;
  output = AST;
  tokenVocab = SchemaLang;
  ASTLabelType = CommonTree;
}

@header{
package eu.reitmayer.schemagen.lang;
}

@members {
	public Schema schema = new Schema();
	public String currentEntity = "";
}

schema_file:
 ^(SCHEMA ID set_property* schema_def*) {schema.setSchemaName($ID.text);}
	;

schema_def:
	  entity_def
	| dom_def
	| elem_def
;



set_property:
	^(SET ID STRING) {schema.setProperty($ID.text, $STRING.text);}
	;
	
entity_def:
	^(ENTITY ID {schema.addEntity($ID.text); currentEntity = $ID.text;} elem_use+)
	;

dom_def returns[String domainID]:
	^(DOMAIN ID (t=TYPE_STRING | t=TYPE_INT | t=TYPE_NUMBER | t=TYPE_TIMESTAMP | t=TYPE_DATE) (LENGTH l=INT (PRECISION p=INT)?)? ) 
	{schema.addDomain($ID.text, $t.text, $l.text, $p.text);
	$domainID = $ID.text;}
	;
	
dom_use returns[String domainID]:
	dom_def {$domainID = $dom_def.domainID;}|
	^(DOMAIN ID) {$domainID = $ID.text;}
	;
	
elem_def returns[String elementID]:
	^(ELEMENT id=ID (STEXT s=STRING)? (MTEXT m=STRING)? (LTEXT l=STRING)? dom_use)
	{
	schema.addElement($id.text, $s.text, $m.text, $l.text, schema.findDomain($dom_use.domainID));
	$elementID = $id.text;
	}
	;
	

key_elem_def returns[String elementID]:
	^(KEY ed=elem_def) {$elementID = $ed.elementID;}|
	^(KEY ne=named_elem) {$elementID = $ne.elementID;}
	;
	
named_elem returns[String elementID]:
	^(ELEMENT ID) {$elementID = $ID.text;}
	;
	
elem_use returns[String elementID, Boolean isKey]:
	elem_def {$elementID = $elem_def.elementID; schema.findEntity(currentEntity).addElement(schema.findElement($elementID), false);}|
	key_elem_def {$elementID = $key_elem_def.elementID; schema.findEntity(currentEntity).addElement(schema.findElement($elementID), true);}|
	named_elem {$elementID = $named_elem.elementID; schema.findEntity(currentEntity).addElement(schema.findElement($elementID), false);}
	;